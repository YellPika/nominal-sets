import RenamingSets.RenameAction.Defs
import Mathlib.Data.Set.Lattice
import Mathlib.Tactic.FunProp

namespace RenamingSets

variable {ğ”¸ X Y Z : Type*} [RenameAction ğ”¸ X] [RenameAction ğ”¸ Y] [RenameAction ğ”¸ Z]

/--
A set `A` is a _support_ of an element `x` if applying any two renamings behaves
identically as long as the renamings agree on `A`.

Intuitively, `A` can be thought of as a superset of `x`'s free variables.
-/
structure IsSupportOf (A : Finset ğ”¸) (x : X) where
  /-- Applying any two renamings behaves identically as long as the renamings agree on `A`. -/
  eq â¦ƒf gâ¦„ : (âˆ€a âˆˆ A, f a = g a) â†’ rename f x = rename g x

/--
An alternative formulation of support for functions.

NOTE: `IsSupportOfF` is actually only equivalent to `IsSupportOf` when the
function in question satisfies `IsSupportedF`.
-/
structure IsSupportOfF (A : Finset ğ”¸) (f : X â†’ Y) where
  /-- Any renaming which does not touch the support commutes with the function. -/
  eq â¦ƒÏƒ : Ren ğ”¸â¦„ : (âˆ€a âˆˆ A, Ïƒ a = a) â†’ âˆ€x, rename Ïƒ (f x) = f (rename Ïƒ x)

variable (ğ”¸) in
/-- An element `x : X` is _supported_ if it has a finite support. -/
inductive IsSupported (x : X) : Prop where
  /-- Applying any two renamings behaves identically as long as the renamings agree on `A`. -/
  | intro (A : Finset ğ”¸) : IsSupportOf A x â†’ IsSupported x

variable (ğ”¸) in
/--
An alternative formulation of supportedness for functions.

NOTE: This is actually a stronger condition that `IsSupported`.
-/
@[fun_prop]
inductive IsSupportedF (f : X â†’ Y) : Prop where
  /-- Any renaming which does not touch the support commutes with the function. -/
  | intro (A : Finset ğ”¸) : IsSupportOfF A f â†’ IsSupportedF f

variable (ğ”¸) in
/-- A function `f : X â†’ Y` is _equivariant_ if it preserves renamings. -/
@[fun_prop]
inductive Equivariant (f : X â†’ Y) : Prop where
  /-- Renamings are preserved by `f`. -/
  | intro : IsSupportOfF (âˆ… : Finset ğ”¸) f â†’ Equivariant f

/--
A (nominal) _renaming set_ is a type with a renaming action such that every
element has a support.
-/
class RenamingSet (ğ”¸ X : Type*) [RenameAction ğ”¸ X] where
  /-- Every element has a support. -/
  isSupported (ğ”¸) (x : X) : IsSupported ğ”¸ x

export RenamingSet (isSupported)

attribute [grind â†, simp] isSupported

/-- Every renaming set has a minimal support, denoted by `supp`. -/
noncomputable def supp (ğ”¸) [RenameAction ğ”¸ X] [RenamingSet ğ”¸ X] (x : X) : Finset ğ”¸ :=
  Set.Finite.toFinset (s := â‹‚A, â‹‚(_ : IsSupportOf A x), A.toSet) (by
    obtain âŸ¨A, hAâŸ© := isSupported ğ”¸ x
    apply Set.Finite.subset
    Â· apply A.finite_toSet
    Â· apply Set.iInter_subset_of_subset A
      simp only [hA, Set.iInter_true, subset_refl])

end RenamingSets
