import RenamingSets.Prod

namespace RenamingSets

variable {ğ”¸ X Y : Type*} [RenameAction ğ”¸ X] [RenameAction ğ”¸ Y]

namespace Finset

@[simps -isSimp]
instance [DecidableEq X] : RenameAction ğ”¸ (Finset X) where
  rename Ïƒ := Finset.image (rename Ïƒ)
  rename_one := by simp only [rename_one', Finset.image_id, implies_true]
  rename_mul f g xs := by
    simp +unfoldPartialApp only [Finset.image_image, Function.comp]
    congr 1
    ext x
    apply rename_mul

@[simp, grind =]
lemma mem_rename
    [DecidableEq X]
    (x : X) (Ïƒ : Ren ğ”¸) (xs : Finset X)
    : x âˆˆ rename Ïƒ xs â†” âˆƒx' âˆˆ xs, rename Ïƒ x' = x := by
  simp only [rename_def, Finset.mem_image]

@[simp, grind â†]
lemma isSupportOf_empty [DecidableEq X] (A : Finset ğ”¸) : IsSupportOf A (âˆ… : Finset X) := by
  simp only [
    isSupportOf_def, Finset.ext_iff, mem_rename, Finset.notMem_empty,
    false_and, exists_false, implies_true]

@[simp, grind â†]
lemma isSupported_empty [DecidableEq X] : IsSupported ğ”¸ (âˆ… : Finset X) := by
  simp only [isSupported_def, isSupportOf_empty, exists_const]

@[grind â†]
lemma isSupportOf_insert [DecidableEq X]
    {A : Finset ğ”¸} {x : X} {xs : Finset X}
    (hx : IsSupportOf A x) (hxs : IsSupportOf A xs)
    : IsSupportOf A (insert x xs) := by
  simp only [isSupportOf_def] at âŠ¢ hx hxs
  intro f g hfg
  ext a
  specialize hx hfg
  specialize hxs hfg
  simp only [Finset.ext_iff, mem_rename] at hxs
  simp only [mem_rename, Finset.mem_insert, exists_eq_or_imp, hx, hxs]

lemma isSupported_insert [DecidableEq X]
    {x : X} {xs : Finset X}
    (hx : IsSupported ğ”¸ x) (hxs : IsSupported ğ”¸ xs)
    : IsSupported ğ”¸ (insert x xs) := by
  classical
  obtain âŸ¨A, hAâŸ© := hx
  obtain âŸ¨B, hBâŸ© := hxs
  use A âˆª B
  apply isSupportOf_insert <;> grind

lemma rename_mono
    [DecidableEq X] (Ïƒ : Ren ğ”¸)
    : Monotone (rename Ïƒ : Finset X â†’ Finset X) := by
  intro xs ys h
  apply Finset.image_mono
  exact h

variable [RenamingSet ğ”¸ X]

instance [DecidableEq X] : RenamingSet ğ”¸ (Finset X) where
  isSupported xs := by
    classical
    induction xs using Finset.induction with
    | empty => simp only [isSupported_empty]
    | insert a s _ ih => apply isSupported_insert <;> grind

@[simp, grind =]
lemma supp_empty [DecidableEq X] : supp ğ”¸ (âˆ… : Finset X) = âˆ… := by
  ext a
  simp only [mem_supp, isSupportOf_empty, forall_const, Finset.notMem_empty, iff_false, not_forall]
  use âˆ…
  simp only [Finset.notMem_empty, not_false_eq_true]

end Finset

variable [RenamingSet ğ”¸ X] [RenamingSet ğ”¸ Y]

lemma supp_rename_subset
    [Infinite ğ”¸] [DecidableEq ğ”¸] (Ïƒ : Ren ğ”¸) (x : X)
    : supp ğ”¸ (rename Ïƒ x) âŠ† rename Ïƒ (supp ğ”¸ x) := by
  apply supp_min
  constructor
  intro f g hfg
  simp only [
    Finset.mem_rename, RenameAction.rename_def, forall_exists_index,
    and_imp, forall_apply_eq_imp_iffâ‚‚] at hfg
  simp only [rename_mul]
  apply rename_congr
  simp only [Ren.mul_coe]
  exact hfg

lemma supp_rename_subset'
    [Infinite ğ”¸] [DecidableEq ğ”¸] (Ïƒ : Ren ğ”¸) (x : X)
    : âˆ€ a âˆˆ supp ğ”¸ (rename Ïƒ x), âˆƒ b âˆˆ supp ğ”¸ x, Ïƒ b = a := by
  intro a ha
  have := supp_rename_subset _ _ ha
  simp only [Finset.mem_rename, RenameAction.rename_def] at this
  exact this

@[simp, grind =]
lemma supp_rename
    [Infinite ğ”¸] [DecidableEq ğ”¸]
    (Ïƒ : Ren ğ”¸) (x : X) (hÏƒ : (supp ğ”¸ x).toSet.InjOn Ïƒ)
    : supp ğ”¸ (rename Ïƒ x) = rename Ïƒ (supp ğ”¸ x) := by
  apply le_antisymm
  Â· simp only [Finset.le_eq_subset, supp_rename_subset]
  Â· intro a ha
    simp only [Finset.mem_rename, RenameAction.rename_def] at ha
    rcases ha with âŸ¨b, hb, rflâŸ©

    let Ïƒ' : Ren ğ”¸ := {
      toFun a := if h : âˆƒb âˆˆ supp ğ”¸ x, a = Ïƒ b then h.choose else a
      exists_support' := by
        use (supp ğ”¸ x).image Ïƒ
        intro a ha
        simp only [Finset.mem_image, not_exists, not_and] at ha
        have : Â¬âˆƒ b âˆˆ supp ğ”¸ x, a = Ïƒ b := by grind
        simp only [this, â†“reduceDIte]
    }
    have Ïƒ'_coe {a} : Ïƒ' a = if h : âˆƒb âˆˆ supp ğ”¸ x, a = Ïƒ b then h.choose else a := by rfl
    have : rename Ïƒ' (rename Ïƒ x) = x := by
      simp only [rename_mul]
      nth_rw 2 [â†rename_one (ğ”¸ := ğ”¸) x]
      apply rename_congr
      simp only [Ren.mul_coe, Ren.one_coe]
      intro a ha
      have : âˆƒ b âˆˆ supp ğ”¸ x, Ïƒ a = Ïƒ b := by use a
      simp only [Ïƒ'_coe, this, â†“reduceDIte]
      apply hÏƒ
      Â· exact this.choose_spec.1
      Â· exact ha
      Â· exact this.choose_spec.2.symm
    rw [â†this] at hb

    have := supp_rename_subset _ _ hb
    simp only [Finset.mem_rename, RenameAction.rename_def] at this
    rcases this with âŸ¨c, hc, rflâŸ©

    have := supp_rename_subset _ _ hc
    simp only [Finset.mem_rename, RenameAction.rename_def] at this
    rcases this with âŸ¨d, hd, rflâŸ©

    have : âˆƒ b âˆˆ supp ğ”¸ x, Ïƒ d = Ïƒ b := by grind
    simp only [Ïƒ'_coe, this, â†“reduceDIte, â†this.choose_spec.2, hc]

lemma isSupportOf_def'
    [Infinite ğ”¸] (A : Finset ğ”¸) (x : X)
    : IsSupportOf A x
    â†” (âˆ€â¦ƒfâ¦„, (âˆ€a âˆˆ A, f a = a) â†’ rename f x = x) := by
  apply Iff.intro
  Â· simp only [isSupportOf_def]
    intro h f hf
    nth_rw 2 [â†rename_one (ğ”¸ := ğ”¸) x]
    apply h
    simp_all only [Ren.one_coe, implies_true]
  Â· classical
    intro h
    suffices supp ğ”¸ x âŠ† A by
      apply isSupportOf_mono
      Â· apply this
      Â· simp only [isSupportOf_supp]
    intro a ha
    obtain âŸ¨b, hbâŸ© := (supp ğ”¸ x).exists_notMem
    have : a âˆ‰ supp ğ”¸ (rename (.restrict {a} fun _ â†¦ b) x) := by
      intro ha
      replace ha := supp_rename_subset _ _ ha
      simp only [
        Finset.mem_rename, RenameAction.rename_def,
        Ren.restrict_coe, Finset.mem_singleton] at ha
      grind

    specialize @h (.restrict {a} fun _ â†¦ b)
    simp only [Ren.restrict_coe, Finset.mem_singleton, ite_eq_right_iff] at h
    grind

lemma supp_apply
    [Infinite ğ”¸] [DecidableEq ğ”¸]
    {A : Finset ğ”¸} {f : X â†’ Y} (hf : IsSupportOfF A f) (x)
    : supp ğ”¸ (f x) âŠ† A âˆª supp ğ”¸ x := by
  classical
  rcases hf with âŸ¨hfâŸ©
  intro a ha
  by_contra! ha'
  obtain âŸ¨b, hbâŸ© := (A âˆª {a}).exists_notMem
  have hx : rename (Ren.restrict {a} fun _ â†¦ b) x = x := by
    apply rename_congr'
    simp only [Ren.restrict_coe, Finset.mem_singleton, ite_eq_right_iff]
    grind
  have := hf
    (Ïƒ := .restrict {a} fun _ â†¦ b)
    (by simp only [Ren.restrict_coe, Finset.mem_singleton, ite_eq_right_iff]
        grind)
    (x := x)
  rw [hx] at this
  rw [â†this] at ha
  replace ha := supp_rename_subset' _ _ _ ha
  simp only [Ren.restrict_coe, Finset.mem_singleton] at ha
  grind

end RenamingSets
