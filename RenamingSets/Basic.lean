import RenamingSets.Defs
import Mathlib.Data.Finset.Fold
import Mathlib.Data.Finset.Lattice.Basic
import Mathlib.Data.Finset.Powerset

namespace RenamingSets

variable {ğ”¸ X Y Z : Type*} [RenameAction ğ”¸ X] [RenameAction ğ”¸ Y] [RenameAction ğ”¸ Z]

/-! ## `Ren` -/

namespace Ren

@[simp]
lemma exists_support (Ï : Ren ğ”¸) : âˆƒA : Finset ğ”¸, âˆ€a âˆ‰ A, Ï a = a :=
  Ï.exists_support'

end Ren

/-! ## `RenameAction` -/

@[simp]
lemma rename_one' : rename (1 : Ren ğ”¸) = (id : X â†’ X) := by
  ext a
  simp only [rename_one, id_eq]

/-- ## `Equivariant` -/

lemma equivariant_def (f : X â†’ Y)
    : Equivariant ğ”¸ f â†” âˆ€ (Ïƒ : Ren ğ”¸) x, rename Ïƒ (f x) = f (rename Ïƒ x) := by
  apply Iff.intro
  Â· apply Equivariant.eq
  Â· apply Equivariant.mk

@[simp, fun_prop]
lemma equivariant_id : Equivariant ğ”¸ (id : X â†’ X) := by
  simp only [equivariant_def, id_eq, implies_true]

@[simp, fun_prop]
lemma equivariant_id' : Equivariant ğ”¸ (fun x : X â†¦ x) := by
  simp only [equivariant_def, implies_true]

@[simp, fun_prop]
lemma equivariant_comp
    {f : Y â†’ Z} (hf : Equivariant ğ”¸ f)
    {g : X â†’ Y} (hg : Equivariant ğ”¸ g)
    : Equivariant ğ”¸ (f âˆ˜ g) := by
  simp_all only [equivariant_def, Function.comp_apply, implies_true]

@[simp, fun_prop]
lemma equivariant_comp'
    {f : Y â†’ Z} (hf : Equivariant ğ”¸ f)
    {g : X â†’ Y} (hg : Equivariant ğ”¸ g)
    : Equivariant ğ”¸ (fun x â†¦ f (g x)) := by
  simp_all only [equivariant_def, implies_true]

/-! ## `IsSupportOf` -/

lemma isSupportOf_def
    (A : Finset ğ”¸) (x : X)
    : IsSupportOf A x
    â†” âˆ€â¦ƒf gâ¦„, (âˆ€a âˆˆ A, f a = g a) â†’ rename f x = rename g x := by
  apply Iff.intro
  Â· apply IsSupportOf.eq
  Â· apply IsSupportOf.mk

lemma isSupportOf_inter
    [DecidableEq ğ”¸]
    {A B : Finset ğ”¸} {x : X}
    (hA : IsSupportOf A x) (hB : IsSupportOf B x)
    : IsSupportOf (A âˆ© B) x := by
  simp_all only [isSupportOf_def, Finset.mem_inter, and_imp]
  intro f g h
  let k : Ren ğ”¸ := {
    toFun a := if a âˆˆ A then f a else g a
    exists_support' := by
      obtain âŸ¨C, hCâŸ© := f.exists_support
      obtain âŸ¨D, hDâŸ© := g.exists_support
      use C âˆª D
      grind
  }
  have k_coe {a} : k a = if a âˆˆ A then f a else g a := rfl
  specialize @hA f k
  specialize @hB k g
  grind

lemma isSupportOf_mono (x : X) : Monotone ((IsSupportOf Â· x) : Finset ğ”¸ â†’ Prop) := by
  intro A B hAB hA
  simp_all only [Finset.le_eq_subset, isSupportOf_def]
  grind

lemma isSupportOf_union_left
    [DecidableEq ğ”¸]
    {A B : Finset ğ”¸} {x : X}
    : IsSupportOf A x â†’ IsSupportOf (A âˆª B) x := by
  apply isSupportOf_mono
  simp only [Finset.le_eq_subset, Finset.subset_union_left]

lemma isSupportOf_union_right
    [DecidableEq ğ”¸]
    {A B : Finset ğ”¸} {x : X}
    : IsSupportOf B x â†’ IsSupportOf (A âˆª B) x := by
  apply isSupportOf_mono
  simp only [Finset.le_eq_subset, Finset.subset_union_right]

/-! ## `supp` -/

variable [RenamingSet ğ”¸ X]

lemma mem_supp
    (a : ğ”¸) (x : X)
    : a âˆˆ supp ğ”¸ x â†” (âˆ€ A : Finset ğ”¸, IsSupportOf A x â†’ a âˆˆ A) := by
  simp only [supp, Set.Finite.mem_toFinset, Set.mem_iInter, Finset.mem_coe]

lemma supp_min {A : Finset ğ”¸} {x : X} (h : IsSupportOf A x) : supp ğ”¸ x âŠ† A := by
  intro a h'
  simp only [mem_supp] at h'
  apply h' A h

@[simp]
lemma isSupportOf_supp
    (ğ”¸) [RenameAction ğ”¸ X] [RenamingSet ğ”¸ X] [Infinite ğ”¸] (x : X)
    : IsSupportOf (supp ğ”¸ x) x := by
  classical
  obtain âŸ¨A, hAâŸ© := exists_support ğ”¸ x

  have : Std.Commutative (Â· âˆ© Â· : Finset ğ”¸ â†’ Finset ğ”¸ â†’ Finset ğ”¸) := by
    constructor
    apply Finset.inter_comm

  have : Std.Associative (Â· âˆ© Â· : Finset ğ”¸ â†’ Finset ğ”¸ â†’ Finset ğ”¸) := by
    constructor
    simp only [Finset.inter_assoc, implies_true]

  have : IsSupportOf
      (Finset.fold (Â· âˆ© Â·) A
        (fun B â†¦ if IsSupportOf B x then B else A)
        A.powerset)
      x := by
    generalize Finset.powerset A = B
    induction B using Finset.induction with
    | empty => simp only [Finset.fold_empty, hA]
    | insert B s ha ih =>
      simp only [ha, not_false_eq_true, Finset.fold_insert]
      apply isSupportOf_inter
      Â· by_cases hB : IsSupportOf B x
        Â· simp only [hB, â†“reduceIte]
        Â· simp only [hB, â†“reduceIte, hA]
      Â· apply ih

  have : supp ğ”¸ x
       = Finset.fold (Â· âˆ© Â·) A (fun B â†¦ if IsSupportOf B x then B else A) A.powerset := by
    rw [subset_antisymm_iff]
    apply And.intro
    Â· apply supp_min
      assumption
    Â· simp only [supp, Set.Finite.subset_toFinset, Set.subset_iInter_iff, Finset.coe_subset]
      intro B hB
      have : A âˆ© B âˆˆ Finset.powerset A := by
        simp only [Finset.mem_powerset, Finset.inter_subset_left]
      have : Finset.powerset A = insert (A âˆ© B) ((Finset.powerset A).erase (A âˆ© B)) := by
        simp only [Finset.mem_powerset, Finset.inter_subset_left, Finset.insert_erase]
      rw [this]
      have : IsSupportOf (A âˆ© B) x := isSupportOf_inter hA hB
      simp only [Finset.mem_erase, ne_eq, not_true_eq_false, Finset.mem_powerset,
        Finset.inter_subset_left, and_true, not_false_eq_true, Finset.fold_insert,
        this, â†“reduceIte, Finset.inter_assoc]
      trans
      Â· apply Finset.inter_subset_right
      Â· apply Finset.inter_subset_left
  rw [this]

  assumption

lemma rename_congr
    [Infinite ğ”¸]
    {f g : Ren ğ”¸} (x : X) (h : âˆ€ a âˆˆ supp ğ”¸ x, f a = g a)
    : rename f x = rename g x := by
  have := isSupportOf_supp ğ”¸ x
  apply this.eq
  exact h

end RenamingSets
