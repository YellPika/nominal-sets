import RenamingSets.Finset

namespace RenamingSets

/--
For any `RenameAction` type `X`, we can form a `RenamingSet` `Restrict ğ”¸ X`
which is simply `X` restricted to those elements which have a finite support.
-/
@[ext]
structure Restrict (ğ”¸ X : Type*) [RenameAction ğ”¸ X] where
  /-- The underlying element. -/
  val : X
  /-- The element has a finite support. -/
  isSupported_val : IsSupported ğ”¸ val

variable {ğ”¸ X Y Z : Type*} [RenameAction ğ”¸ X] [RenameAction ğ”¸ Y] [RenameAction ğ”¸ Z]

namespace Restrict

attribute [coe] val
attribute [simp] isSupported_val

instance : CoeOut (Restrict ğ”¸ X) X where
  coe := val

@[simps]
instance : RenameAction ğ”¸ (Restrict ğ”¸ X) where
  rename Ïƒ x := {
    val := rename Ïƒ x
    isSupported_val := by
      classical
      apply isSupported_rename
      exact x.isSupported_val
  }
  rename_one := by simp only [rename_one, implies_true]
  rename_mul := by simp only [rename_mul, implies_true]

@[simp]
lemma isSupportOf (A : Finset ğ”¸) (x : Restrict ğ”¸ X) : IsSupportOf A x â†” IsSupportOf A x.val := by
  apply Iff.intro <;>
  Â· rintro âŸ¨hâŸ©
    constructor
    simp only [Restrict.ext_iff, rename_val] at âŠ¢ h
    exact h

instance : RenamingSet ğ”¸ (Restrict ğ”¸ X) where
  isSupported x := by
    obtain âŸ¨A, hAâŸ© := x.isSupported_val
    use A
    simp only [isSupportOf, hA]

@[simp, fun_prop]
lemma isSupportedF_val : IsSupportedF ğ”¸ (val : Restrict ğ”¸ X â†’ X) := by
  use âˆ…
  simp only [isSupportOfF_def, Finset.notMem_empty, IsEmpty.forall_iff, implies_true, rename_val]

@[simp]
lemma isSupportedF_iff
    (f : X â†’ Restrict ğ”¸ Y)
    : IsSupportedF ğ”¸ f â†” IsSupportedF ğ”¸ (fun x â†¦ (f x).val) := by
  apply Iff.intro <;>
  Â· rintro âŸ¨A, hAâŸ©
    use A
    simp only [isSupportOfF_def, Restrict.ext_iff, rename_val] at âŠ¢ hA
    exact hA

@[fun_prop]
lemma isSupportedF_mk
    {f : X â†’ Y} (hf : IsSupportedF ğ”¸ f)
    (p : âˆ€ x, IsSupported ğ”¸ (f x))
    : IsSupportedF ğ”¸ fun x â†¦ mk (f x) (p x) := by
  simp only [isSupportedF_iff, hf]

end Restrict

end RenamingSets
