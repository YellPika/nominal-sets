import RenamingSets.Function

namespace RenamingSets

variable {ğ”¸ X Y Z : Type*} [RenameAction ğ”¸ X] [RenameAction ğ”¸ Y] [RenameAction ğ”¸ Z]

/-- Morphisms in the category of nominal renaming sets. -/
structure Hom (ğ”¸ X Y : Type*) [RenameAction ğ”¸ X] [RenameAction ğ”¸ Y] where
  /-- The underlying function. -/
  toFun : X â†’ Y
  /--
  The function has a finite support. Do not use this directly except when
  elements of the `Hom` type. Prefer `isSupportedF` instead.
  -/
  isSupportedF' : IsSupportedF ğ”¸ toFun := by fun_prop

namespace Hom

instance : FunLike (Hom ğ”¸ X Y) X Y where
  coe := toFun
  coe_injective' := by
    intro âŸ¨_, _âŸ© âŸ¨_, _âŸ© h
    simp only [mk.injEq] at âŠ¢ h
    rw [h]

/-- A simps projection for function application. -/
def Simps.coe (f : Hom ğ”¸ X Y) : X â†’ Y := f

initialize_simps_projections Hom (toFun â†’ coe)

@[simp, local fun_prop]
lemma isSupportedF (f : Hom ğ”¸ X Y) : IsSupportedF ğ”¸ f :=
  f.isSupportedF'

@[simp]
lemma coe_mk (f : X â†’ Y) (hf : IsSupportedF ğ”¸ f) : mk (ğ”¸ := ğ”¸) f = f := rfl

@[simp]
lemma mk_coe (f : Hom ğ”¸ X Y) : mk f = f := rfl

@[ext]
lemma ext {f g : Hom ğ”¸ X Y} (h : (f : X â†’ Y) = g) : f = g := by
  rcases f
  rcases g
  simpa only [mk.injEq, coe_mk] using h

lemma ext'
    [Infinite ğ”¸] [DecidableEq ğ”¸] [RenamingSet ğ”¸ X]
    {f g : Hom ğ”¸ X Y} (A : Finset ğ”¸)
    (hA : âˆ€ x, supp ğ”¸ x âˆ© A = âˆ… â†’ f x = g x)
    : f = g := by
  obtain âŸ¨B, hBâŸ© := f.isSupportedF
  obtain âŸ¨C, hCâŸ© := g.isSupportedF
  ext : 1
  apply Function.ext' (A âˆª B âˆª C)
  Â· grind
  Â· grind
  Â· intro x hx
    apply hA
    simp only [
      Finset.union_assoc, Finset.ext_iff, Finset.mem_inter, Finset.mem_union,
      Finset.notMem_empty, iff_false, not_and, not_or] at âŠ¢ hx
    grind

variable [Infinite ğ”¸] [RenamingSet ğ”¸ X] [RenamingSet ğ”¸ Y] [RenamingSet ğ”¸ Z]

@[simps -isSimp]
noncomputable instance : RenameAction ğ”¸ (Hom ğ”¸ X Y) where
  rename Ïƒ f := mk (rename Ïƒ (f : X â†’ Y))
  rename_one f := by simp only [rename_one', id_eq, mk_coe]
  rename_mul Ïƒâ‚ Ïƒâ‚‚ f := by simp only [coe_mk, rename_mul]

lemma isSupportOf_iff_coe
    (A : Finset ğ”¸) (f : Hom ğ”¸ X Y)
    : IsSupportOf A f â†” IsSupportOf A (f : X â†’ Y) := by
  simp only [isSupportOf_def, Hom.ext_iff, rename_coe, funext_iff]

instance : RenamingSet ğ”¸ (Hom ğ”¸ X Y) where
  isSupported f := by
    obtain âŸ¨A, hAâŸ© := f.isSupportedF
    use A
    simp only [isSupportOf_iff_coe]
    apply Function.isSupportOf_of_isSupportOfF
    exact hA

@[simp]
lemma rename_apply
    (Ïƒ : Ren ğ”¸) (f : Hom ğ”¸ X Y) (x : X)
    : rename Ïƒ f (rename Ïƒ x) = rename Ïƒ (f x) := by
  simp only [rename_coe, isSupportedF, Function.rename_apply]

@[simp]
lemma isSupportOfF_supp (f : Hom ğ”¸ X Y) : IsSupportOfF (supp ğ”¸ f) f := by
  constructor
  intro Ïƒ hÏƒ x
  rw [â† rename_apply, rename_congr']
  exact hÏƒ

lemma supp_min {A : Finset ğ”¸} {f : Hom ğ”¸ X Y} (hf : IsSupportOfF A f) : supp ğ”¸ f âŠ† A := by
  intro a ha
  simp only [mem_supp] at ha
  apply ha
  simp only [isSupportOf_iff_coe, hf, Function.isSupportOf_of_isSupportOfF]

@[simp]
lemma isSupportOf_iff_isSupportOfF
    (A : Finset ğ”¸) (f : Hom ğ”¸ X Y)
    : IsSupportOf A f â†” IsSupportOfF A f := by
  apply Iff.intro
  Â· intro hf
    apply isSupportOfF_mono f
    Â· apply RenamingSets.supp_min hf
    Â· simp only [isSupportOfF_supp]
  Â· intro hf
    apply isSupportOf_mono f (supp_min hf)
    simp only [isSupportOf_supp]

lemma supp_subset [DecidableEq ğ”¸]
    (f : Hom ğ”¸ X Y) (x : X)
    : supp ğ”¸ (f x) âŠ† supp ğ”¸ f âˆª supp ğ”¸ x := by
  apply supp_apply
  simp only [isSupportOfF_supp]

@[simp]
lemma equivariant_coe : Equivariant ğ”¸ (fun ((f, x) : Hom ğ”¸ X Y Ã— X) â†¦ f x) := by
  simp only [
    equivariant_def, isSupportOfF_def, Finset.notMem_empty, IsEmpty.forall_iff,
    implies_true, Prod.forall, Prod.rename_mk, rename_apply]

@[simp]
lemma isSupportedF_apply : IsSupportedF ğ”¸ (fun ((f, x) : Hom ğ”¸ X Y Ã— X) â†¦ f x) := by
  apply isSupportedF_of_equivariant
  simp only [equivariant_coe]

omit [RenamingSet ğ”¸ X] in
@[fun_prop, simp]
lemma equivariant_coe'
    {f : X â†’ Hom ğ”¸ Y Z} (hf : Equivariant ğ”¸ f)
    {g : X â†’ Y} (hg : Equivariant ğ”¸ g)
    : Equivariant ğ”¸ (fun x â†¦ f x (g x)) := by
  have := equivariant_comp' (ğ”¸ := ğ”¸)
    (f := fun ((f, x) : Hom ğ”¸ Y Z Ã— Y) â†¦ f x)
    (g := fun x â†¦ (f x, g x))
  simp only [equivariant_coe, forall_const] at this
  apply this
  fun_prop

omit [RenamingSet ğ”¸ X] in
@[fun_prop, simp]
lemma isSupportedF''
    {f : X â†’ Hom ğ”¸ Y Z} (hf : IsSupportedF ğ”¸ f)
    {g : X â†’ Y} (hg : IsSupportedF ğ”¸ g)
    : IsSupportedF ğ”¸ (fun x â†¦ f x (g x)) := by
  have := isSupportedF_comp' (ğ”¸ := ğ”¸)
    (f := fun ((f, x) : Hom ğ”¸ Y Z Ã— Y) â†¦ f x)
    (g := fun x â†¦ (f x, g x))
  simp only [isSupportedF_apply, forall_const] at this
  apply this
  fun_prop

@[fun_prop]
lemma isSupportedF_mk
    (f : X â†’ Y â†’ Z) (hf : IsSupportedF ğ”¸ fun (x, y) â†¦ f x y)
    : IsSupportedF ğ”¸ (fun x â†¦ mk (f x)) := by
  classical
  obtain âŸ¨A, hAâŸ© := hf
  use A
  simp only [isSupportOfF_def, Prod.forall, Prod.rename_mk] at âŠ¢ hA
  intro Ïƒ hÏƒ x
  apply ext' (supp ğ”¸ x âˆª A âˆª Ïƒ.supp)
  intro y hy
  have : rename Ïƒ y = y := by
    apply rename_congr'
    simp only [Finset.union_assoc, Finset.ext_iff, Finset.mem_inter, Finset.mem_union,
      Ren.mem_supp, ne_eq, Finset.notMem_empty, iff_false, not_and, not_or,
      Decidable.not_not] at hy
    grind
  rw [â†this, rename_apply]
  simp only [coe_mk]
  apply hA hÏƒ

/-- Currying for morphisms. -/
@[simps]
def curry (f : Hom ğ”¸ (X Ã— Y) Z) : Hom ğ”¸ X (Hom ğ”¸ Y Z) where
  toFun x := { toFun y := f (x, y) }

/-- The evaluation morphism. -/
@[simps]
def eval : Hom ğ”¸ (Hom ğ”¸ X Y Ã— X) Y where
  toFun x := x.1 x.2

end Hom

end RenamingSets
