import RenamingSets.PartialHom
import RenamingSets.Ren.Base

namespace RenamingSets

variable {ğ”¸ X Y Z : Type*} [RenameAction ğ”¸ X] [RenameAction ğ”¸ Y] [RenameAction ğ”¸ Z]

/-- Morphisms in the category of nominal renaming sets. -/
structure Hom (ğ”¸ X Y : Type*) [RenameAction ğ”¸ X] [RenameAction ğ”¸ Y] where
  /-- The underlying function. -/
  toFun : X â†’ Y
  /-- The function has a finite support. -/
  exists_support' : âˆƒ A : Finset ğ”¸,
    âˆ€ â¦ƒÏƒâ¦„, (âˆ€a âˆˆ A, Ïƒ a = a) â†’
    âˆ€ â¦ƒxâ¦„, rename Ïƒ (toFun x) = toFun (rename Ïƒ x)

namespace Hom

instance : FunLike (Hom ğ”¸ X Y) X Y where
  coe := toFun
  coe_injective' := by
    intro âŸ¨_, _âŸ© âŸ¨_, _âŸ© h
    simp only [mk.injEq] at âŠ¢ h
    rw [h]

@[simp]
lemma coe_mk (f : X â†’ Y) (hf : _) (x : X) : mk (ğ”¸ := ğ”¸) f hf x = f x := rfl

lemma exists_support (f : Hom ğ”¸ X Y) : âˆƒ A : Finset ğ”¸,
    âˆ€ â¦ƒÏƒâ¦„, (âˆ€a âˆˆ A, Ïƒ a = a) â†’
    âˆ€ â¦ƒxâ¦„, rename Ïƒ (f x) = f (rename Ïƒ x) := by
  exact f.exists_support'

lemma exists_support_subset
    [RenamingSet ğ”¸ X] [RenamingSet ğ”¸ Y] [Infinite ğ”¸] [DecidableEq ğ”¸]
    (f : Hom ğ”¸ X Y) (x)
    : supp ğ”¸ (f x) âŠ† f.exists_support.choose âˆª supp ğ”¸ x := by
  classical
  intro a ha
  by_contra! ha'
  obtain âŸ¨b, hbâŸ© := (f.exists_support.choose âˆª {a}).exists_notMem
  have hx : rename (Ren.restrict {a} fun _ â†¦ b) x = x := by
    apply rename_congr'
    simp only [Ren.restrict_coe, Finset.mem_singleton, ite_eq_right_iff]
    grind
  have := f.exists_support.choose_spec
    (Ïƒ := .restrict {a} fun _ â†¦ b)
    (by simp only [Ren.restrict_coe, Finset.mem_singleton, ite_eq_right_iff]
        grind)
    (x := x)
  rw [hx] at this
  rw [â†this] at ha
  replace ha := supp_rename_subset' _ _ _ ha
  simp only [Ren.restrict_coe, Finset.mem_singleton] at ha
  grind

@[ext]
lemma ext {f g : Hom ğ”¸ X Y} (h : âˆ€ x, f x = g x) : f = g := by
  rcases f
  rcases g
  simp only [coe_mk, mk.injEq] at âŠ¢ h
  ext x
  apply h

lemma ext'
    [Infinite ğ”¸] [DecidableEq ğ”¸] [RenamingSet ğ”¸ X]
    {f g : Hom ğ”¸ X Y} (A : Finset ğ”¸)
    (hA : âˆ€ x, supp ğ”¸ x âˆ© A = âˆ… â†’ f x = g x)
    : f = g := by
  obtain âŸ¨B, hBâŸ© := f.exists_support
  obtain âŸ¨C, hCâŸ© := g.exists_support
  ext x
  let D := supp ğ”¸ x âˆª B âˆª C âˆª A
  have : rename (.unfresh (supp ğ”¸ x) D * .fresh (supp ğ”¸ x) D) x = x := by
    apply rename_congr'
    simp only [Ren.mul_coe]
    grind
  rw [â† this]
  simp only [â† rename_mul]
  rw [â† hB, â† hC]
  Â· congr 1
    apply hA
    ext a
    simp only [
      Ren.fresh_injOn, supp_rename, Finset.mem_inter,
      Finset.mem_rename, rename_def, Finset.notMem_empty,
      iff_false, not_and, forall_exists_index, and_imp]
    grind
  Â· grind
  Â· grind

variable [DecidableEq ğ”¸] [Infinite ğ”¸] [RenamingSet ğ”¸ X] [RenamingSet ğ”¸ Y]

/-- Every `PartialHom` can be uniquely extended to a compatible `Hom`. -/
noncomputable irreducible_def extend {A : Finset ğ”¸} (f : PartialHom A X Y) : Hom ğ”¸ X Y where
  toFun x := f.extend x
  exists_support' := by
    use A
    grind

@[simp, grind =]
lemma extend_eq
    {A : Finset ğ”¸} (f : PartialHom A X Y) {x} (hx : supp ğ”¸ x âˆ© A = âˆ…)
    : extend f x = f âŸ¨x, hxâŸ© := by
  simp only [extend_def, coe_mk, hx, PartialHom.extend_eq]

@[simps]
private def renameâ‚€
    (Ïƒ : Ren ğ”¸) (f : Hom ğ”¸ X Y)
    : PartialHom (f.exists_support.choose âˆª Ïƒ.supp âˆª f.exists_support.choose.image Ïƒ) X Y where
  toFun x := rename Ïƒ (f x)
  supported' Ïƒ' hÏƒ' x hxâ‚ hxâ‚‚ := by
    dsimp only

    let Î¼ : Ren ğ”¸ := .base (supp ğ”¸ x) Ïƒ'

    have lemmaâ‚ : rename Ïƒ' x = rename Ïƒ' (rename Î¼ x) := by
      simp only [rename_mul]
      apply rename_congr
      grind
    rw [lemmaâ‚]

    have lemmaâ‚‚ : f (rename Ïƒ' (rename Î¼ x)) = rename Ïƒ' (f (rename Î¼ x)) := by
      rw [f.exists_support.choose_spec]
      grind
    rw [lemmaâ‚‚]

    have lemmaâ‚ƒ
        : rename Ïƒ (rename Ïƒ' (f (rename Î¼ x)))
        = rename Ïƒ' (rename Ïƒ (f (rename Î¼ x))) := by
      simp only [rename_mul]
      apply rename_congr
      intro a ha
      simp only [Ren.mul_coe]
      have := exists_support_subset f _ ha
      simp only [Finset.mem_union] at this
      cases this with
      | inl this => grind
      | inr this =>
        have hâ‚ : Ïƒ' a âˆˆ supp ğ”¸ (rename Ïƒ' x) := by
          rw [lemmaâ‚, supp_rename]
          Â· simp only [Finset.mem_rename, rename_def]
            use a
          Â· intro b hb c hc hbc
            rcases supp_rename_subset' _ _ _ hb with âŸ¨b, hb', rflâŸ©
            rcases supp_rename_subset' _ _ _ hc with âŸ¨c, hc', rflâŸ©
            grind [= Ren.restrict_coe]
        have hâ‚‚ : a âˆˆ supp ğ”¸ x := by
          rcases supp_rename_subset' _ _ _ this with âŸ¨a, ha', rflâŸ©
          grind
        have hâ‚ƒ : Ïƒ (Ïƒ' a) = Ïƒ' a âˆ§ Ïƒ a = a := by
          simp only [
            Finset.union_assoc, Finset.ext_iff, Finset.mem_inter,
            Finset.mem_union, Ren.mem_supp] at hxâ‚ hxâ‚‚
          grind
        simp only [hâ‚ƒ]
    rw [lemmaâ‚ƒ]

    have lemmaâ‚„ : f (rename Î¼ x) = rename Î¼ (f x) := by
      rw [f.exists_support.choose_spec]
      simp only [Finset.ext_iff, Finset.mem_inter] at hxâ‚
      grind
    rw [lemmaâ‚„]

    have lemmaâ‚… : rename Ïƒ (rename Î¼ (f x)) = rename Î¼ (rename Ïƒ (f x)) := by
      simp only [rename_mul]
      apply rename_congr
      intro a ha
      replace ha := exists_support_subset f _ ha
      simp only [Finset.mem_union] at ha
      cases ha with
      | inl ha =>
        simp only [Ren.mul_coe]
        have : a âˆ‰ supp ğ”¸ x := by
          simp only [Finset.ext_iff, Finset.mem_inter] at hxâ‚
          grind
        have : Ïƒ a âˆ‰ supp ğ”¸ x := by
          simp only [Finset.ext_iff, Finset.mem_inter] at hxâ‚
          grind
        grind
      | inr ha =>
        have : Ïƒ a = a âˆ§ Ïƒ (Î¼ a) = Î¼ a := by
          simp only [
            Finset.union_assoc, Finset.ext_iff, Finset.mem_inter,
            Finset.mem_union, Ren.mem_supp] at hxâ‚
          grind
        simp only [Ren.mul_coe, this]
    rw [lemmaâ‚…]

    simp only [rename_mul, Ren.mul_base_r, Î¼]

@[simps -isSimp]
noncomputable instance : RenameAction ğ”¸ (Hom ğ”¸ X Y) where
  rename Ïƒ f := extend (renameâ‚€ Ïƒ f)

  rename_one f := by
    apply ext' f.exists_support.choose
    intro x hx
    rw [extend_eq]
    Â· simp only [renameâ‚€_toFun, rename_one', id_eq]
    Â· ext a
      simp only [Finset.union_assoc, Finset.mem_inter, Finset.mem_union, Ren.mem_supp, Ren.one_coe,
        ne_eq, not_true_eq_false, Finset.mem_image, exists_eq_right, Finset.notMem_empty, iff_false,
        not_and]
      simp only [Finset.ext_iff, Finset.mem_inter, Finset.notMem_empty, iff_false, not_and] at hx
      grind

  rename_mul Ïƒâ‚ Ïƒâ‚‚ f := by
    apply ext' <|
      (extend (renameâ‚€ Ïƒâ‚‚ f)).exists_support.choose âˆª
      (extend (renameâ‚€ Ïƒâ‚‚ f)).exists_support.choose.image Ïƒâ‚ âˆª
      f.exists_support.choose âˆª
      f.exists_support.choose.image (Ïƒâ‚ * Ïƒâ‚‚) âˆª
      f.exists_support.choose.image Ïƒâ‚‚ âˆª
      Ïƒâ‚.supp âˆª
      Ïƒâ‚‚.supp âˆª
      (Ïƒâ‚ * Ïƒâ‚‚).supp
    intro a hx
    simp only [Finset.union_assoc, Finset.ext_iff, Finset.mem_inter, Finset.mem_union,
      Finset.mem_image, Ren.mul_coe, Ren.mem_supp, ne_eq, Finset.notMem_empty, iff_false, not_and,
      not_or, not_exists, Decidable.not_not] at hx
    rw [extend_eq, extend_eq, renameâ‚€_toFun, extend_eq]
    Â· simp only [renameâ‚€_toFun, rename_mul]
    Â· ext a
      simp only [Finset.union_assoc, Finset.mem_inter, Finset.mem_union, Finset.mem_image,
        Ren.mem_supp, ne_eq, Finset.notMem_empty, iff_false, not_and, not_or, not_exists,
        Decidable.not_not]
      grind
    Â· ext a
      simp only [Finset.union_assoc, Finset.mem_inter, Finset.mem_union, Finset.mem_image,
        Ren.mul_coe, Ren.mem_supp, ne_eq, Finset.notMem_empty, iff_false, not_and, not_or,
        not_exists, Decidable.not_not]
      grind
    Â· ext a
      simp only [Finset.union_assoc, Finset.mem_inter, Finset.mem_union, Finset.mem_image,
        Ren.mem_supp, ne_eq, Finset.notMem_empty, iff_false, not_and, not_or, not_exists,
        Decidable.not_not]
      grind

instance : RenamingSet ğ”¸ (Hom ğ”¸ X Y) where
  exists_support f := by
    classical
    use f.exists_support.choose
    simp only [isSupportOf_def, rename_def]
    intro Ïƒâ‚ Ïƒâ‚‚ hÏƒ
    apply ext' <|
      f.exists_support.choose
        âˆª Ïƒâ‚.supp
        âˆª Ïƒâ‚‚.supp
        âˆª f.exists_support.choose.image Ïƒâ‚
        âˆª f.exists_support.choose.image Ïƒâ‚‚
    intro x hx
    rw [extend_eq, extend_eq]
    Â· dsimp only [renameâ‚€_toFun]
      apply rename_congr
      intro a ha
      replace ha := exists_support_subset f _ ha
      simp only [Finset.mem_union] at ha
      cases ha with
      | inl h => exact hÏƒ _ h
      | inr h =>
        have : Ïƒâ‚ a = a âˆ§ Ïƒâ‚‚ a = a := by
          simp only [
            Finset.union_assoc, Finset.ext_iff, Finset.mem_inter,
            Finset.mem_union, Ren.mem_supp] at hx
          grind
        grind
    Â· grind
    Â· grind

@[simp]
lemma rename_apply
    (Ïƒ : Ren ğ”¸) (f : Hom ğ”¸ X Y) (x : X)
    : rename Ïƒ f (rename Ïƒ x) = rename Ïƒ (f x) := by
  classical
  generalize hA
    : f.exists_support.choose
        âˆª (rename Ïƒ f).exists_support.choose
        âˆª Ïƒ.supp
        âˆª supp ğ”¸ x
        âˆª (supp ğ”¸ x).image Ïƒ
        âˆª f.exists_support.choose.image Ïƒ
    = A
  let Ï„ := Ren.fresh A A
  have : âˆ€a âˆˆ A, Ïƒ (Ï„ a) = Ï„ a := by
    intro a ha
    have : Ï„ a âˆ‰ Ïƒ.supp := by grind
    simpa only [Ren.mem_supp, ne_eq, Decidable.not_not] using this
  let Ï„â‚œ := Ren.unfresh A A
  let Ï€ : Ren ğ”¸ := {
    toFun a := if a âˆˆ A then Ï„ a else if âˆƒb âˆˆ A, a = Ï„ b then Ï„â‚œ a else a
    exists_support' := by
      use A âˆª A.image Ï„
      intro a ha
      simp only [Finset.mem_union, Finset.mem_image, not_or, not_exists, not_and] at ha
      grind
  }
  have Ï€_coe {a} : Ï€ a = if a âˆˆ A then Ï„ a else if âˆƒb âˆˆ A, a = Ï„ b then Ï„â‚œ a else a := rfl
  let Ïƒ' := Ï€ * Ïƒ * Ï€
  have : âˆ€a âˆˆ supp ğ”¸ x, (Ï„â‚œ * Ïƒ' * Ï„) a = Ïƒ a := by
    intro a ha
    have hâ‚ : Ren.fresh A A a âˆ‰ A := by grind
    have hâ‚‚ : âˆƒ b âˆˆ A, Ren.fresh A A a = Ren.fresh A A b := by grind
    have hâ‚ƒ : Ren.unfresh A A (Ren.fresh A A a) = a := by grind
    have hâ‚„ : Ïƒ a âˆˆ A := by grind
    simp only [Ren.mul_coe, Ï€_coe, hâ‚, â†“reduceIte, hâ‚‚, hâ‚ƒ, hâ‚„, Ren.unfresh_freshâ‚, Ï„â‚œ, Ïƒ', Ï„]
  have : âˆ€a âˆˆ supp ğ”¸ (f (rename Ï„ x)), (Ï„â‚œ * Ïƒ' * Ïƒ) a = (Ïƒ * Ï„â‚œ) a := by
    intro a ha
    simp only [Ren.mul_coe, Ï€_coe, Ïƒ']
    replace ha := exists_support_subset f _ ha
    simp only [Finset.mem_union] at ha
    cases ha with
    | inl ha =>
      replace ha : Ïƒ a âˆˆ A := by grind
      have hâ‚ : Ïƒ (Ï„ (Ïƒ a)) = Ï„ (Ïƒ a) := by grind
      have hâ‚‚ : Ï„ (Ï„ (Ïƒ a)) = Ï„ (Ïƒ a) := by grind
      have hâ‚ƒ : âˆƒ b âˆˆ A, Ï„ (Ïƒ a) = Ï„ b := by grind
      have hâ‚„ : Ï„â‚œ (Ï„ (Ïƒ a)) = Ïƒ a := by grind
      have hâ‚… : Ï„â‚œ a = a := by grind
      have hâ‚† : Ï„â‚œ (Ïƒ a) = (Ïƒ a) := by grind
      simp only [ha, â†“reduceIte, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, apply_ite, hâ‚†, ite_self, hâ‚…]
    | inr ha =>
      have ha' := supp_rename_subset' _ _ _ ha
      rcases ha' with âŸ¨a, ha', rflâŸ©
      have hâ‚ : Ïƒ (Ï„ a) = Ï„ a := by grind
      have hâ‚‚ : Ï„ a âˆ‰ A := by grind
      have hâ‚ƒ : âˆƒ b âˆˆ A, Ï„ a = Ï„ b := by grind
      have hâ‚„ : Ï„â‚œ (Ï„ a) = a := by grind
      have hâ‚… : Ïƒ a âˆˆ A := by grind
      have hâ‚† : Ï„â‚œ (Ï„ (Ïƒ a)) = (Ïƒ a) := by grind
      simp only [hâ‚, hâ‚‚, â†“reduceIte, hâ‚ƒ, hâ‚„, hâ‚…, hâ‚†]
  have : âˆ€a âˆˆ (rename Ïƒ f).exists_support.choose, (Ï„â‚œ * Ïƒ') a = a := by
    intro a ha
    have hâ‚ : a âˆˆ A := by grind
    have hâ‚‚ : Ïƒ (Ï„ a) = Ï„ a := by grind
    have hâ‚ƒ : Ï„ a âˆ‰ A := by grind
    have hâ‚„ : âˆƒ b âˆˆ A, Ï„ a = Ï„ b := by grind
    simp only [Ren.mul_coe, Ï€_coe, hâ‚, â†“reduceIte, Ïƒ', hâ‚‚, hâ‚ƒ, hâ‚„]
    grind
  have : rename Ïƒ x = rename (Ï„â‚œ * Ïƒ') (rename Ï„ x) := by
    simp only [rename_mul]
    apply rename_congr
    grind
  have : (rename Ïƒ f) (rename (Ï„â‚œ * Ïƒ') (rename Ï„ x))
       = rename (Ï„â‚œ * Ïƒ') (rename Ïƒ f (rename Ï„ x)) := by
    simp only [rename_mul]
    rw [(rename Ïƒ f).exists_support.choose_spec (by grind)]
    simp only [rename_mul]
  have : rename Ïƒ f (rename Ï„ x) = rename Ïƒ (f (rename Ï„ x)) := by
    simp only [rename_def]
    rw [extend_eq]
    Â· simp only [renameâ‚€_toFun]
    Â· ext a
      simp only [Finset.union_assoc, Finset.mem_inter, Finset.mem_union, Ren.mem_supp, ne_eq,
        Finset.mem_image, Finset.notMem_empty, iff_false, not_and, not_or, Decidable.not_not,
        not_exists]
      intro ha
      replace ha := supp_rename_subset' _ _ _ ha
      rcases ha with âŸ¨a, ha, rflâŸ©
      have : Ï„ a âˆ‰ A := by grind
      refine âŸ¨?_, ?_, ?_âŸ©
      Â· grind
      Â· have : Ï„ a âˆ‰ Ïƒ.supp := by grind
        simpa only [Ren.mem_supp, ne_eq, Decidable.not_not] using this
      Â· grind
  have : rename (Ï„â‚œ * Ïƒ') (rename Ïƒ (f (rename Ï„ x))) = rename (Ïƒ * Ï„â‚œ) (f (rename Ï„ x)) := by
    simp only [rename_mul]
    apply rename_congr
    grind
  have : rename (Ïƒ * Ï„â‚œ) (f (rename Ï„ x)) = rename Ïƒ (f (rename Ï„â‚œ (rename Ï„ x))) := by
    nth_rw 2 [â†f.exists_support.choose_spec]
    Â· simp only [rename_mul]
    Â· grind
  have : rename Ï„â‚œ (rename Ï„ x) = x := by
    simp only [rename_mul]
    apply rename_congr'
    simp only [Ren.mul_coe]
    grind
  grind

lemma supp_subset_iff
    (f : Hom ğ”¸ X Y) (A : Finset ğ”¸)
    : supp ğ”¸ f âŠ† A
    â†” âˆ€ Ïƒ, (âˆ€ a âˆˆ A, Ïƒ a = a) â†’ âˆ€ x, rename Ïƒ (f x) = f (rename Ïƒ x) := by
  apply Iff.intro
  Â· intro h Ïƒ hÏƒ x
    rw [â† rename_apply, rename_congr']
    grind
  Â· intro h
    suffices IsSupportOf A f by exact supp_min this
    simp only [isSupportOf_def']
    intro Ïƒ hÏƒ
    apply ext' Ïƒ.supp
    intro x hx
    have : âˆ€a âˆˆ supp ğ”¸ x, Ïƒ a = a := by
      simp only [Finset.ext_iff, Finset.mem_inter, Ren.mem_supp] at hx
      grind
    have : f x = f (rename Ïƒ x) := by
      rw [rename_congr']
      grind
    have : rename Ïƒ (f x) = rename Ïƒ f (rename Ïƒ x) := by
      rw [rename_apply]
    have : rename Ïƒ x = x := by
      apply rename_congr'
      grind
    grind

lemma supp_subset
    (f : Hom ğ”¸ X Y) (x : X)
    : supp ğ”¸ (f x) âŠ† supp ğ”¸ f âˆª supp ğ”¸ x := by
  intro a ha
  have : âˆ€ Ïƒ, (âˆ€ a âˆˆ supp ğ”¸ f, Ïƒ a = a) â†’ âˆ€ x, rename Ïƒ (f x) = f (rename Ïƒ x) := by
    rw [â† supp_subset_iff]
  simp only [Finset.mem_union]
  by_contra! ha'
  obtain âŸ¨b, hbâŸ© := (supp ğ”¸ f âˆª {a}).exists_notMem
  specialize this
    (.restrict {a} fun _ â†¦ b)
    (by simp only [Ren.restrict_coe, Finset.mem_singleton, ite_eq_right_iff]
        grind)
    x
  have hx : (rename (Ren.restrict {a} fun x â†¦ b) x) = x := by
    apply rename_congr'
    simp only [Ren.restrict_coe, Finset.mem_singleton, ite_eq_right_iff]
    grind
  rw [hx] at this
  rw [â†this] at ha
  replace ha := supp_rename_subset' _ _ _ ha
  simp only [Ren.restrict_coe, Finset.mem_singleton] at ha
  grind

@[simps]
private def curryâ‚€
    [RenamingSet ğ”¸ X] [RenamingSet ğ”¸ Y] [RenamingSet ğ”¸ Z]
    (f : Hom ğ”¸ (X Ã— Y) Z) (x : X)
    : Hom ğ”¸ Y Z where
  toFun y := f (x, y)
  exists_support' := by
    use supp ğ”¸ f âˆª supp ğ”¸ x
    intro Ïƒ hÏƒ y
    simp only [â† rename_apply, Prod.rename_mk]
    rw [rename_congr', rename_congr']
    Â· grind
    Â· grind

/-- Currying for morphisms. -/
@[simps!]
def curry
    [Infinite ğ”¸] [DecidableEq ğ”¸]
    [RenamingSet ğ”¸ X] [RenamingSet ğ”¸ Y] [RenamingSet ğ”¸ Z]
    (f : Hom ğ”¸ (X Ã— Y) Z)
    : Hom ğ”¸ X (Hom ğ”¸ Y Z) where
  toFun := curryâ‚€ f
  exists_support' := by
    use supp ğ”¸ f
    intro Ïƒ hÏƒ x
    apply ext' (supp ğ”¸ x âˆª supp ğ”¸ f âˆª Ïƒ.supp)
    intro y hy
    simp only [curryâ‚€_toFun]
    have : rename Ïƒ y = y := by
      apply rename_congr'
      simp only [Finset.union_assoc, Finset.ext_iff, Finset.mem_inter, Finset.mem_union,
        Ren.mem_supp, ne_eq, Finset.notMem_empty, iff_false, not_and, not_or,
        Decidable.not_not] at hy
      grind
    rw [â†this, rename_apply]
    simp only [curryâ‚€_toFun, â† rename_apply, Prod.rename_mk]
    rw [rename_congr']
    grind

/-- The evaluation morphism. -/
@[simps]
def eval
    [Infinite ğ”¸] [DecidableEq ğ”¸]
    [RenamingSet ğ”¸ X] [RenamingSet ğ”¸ Y] [RenamingSet ğ”¸ Z]
    : Hom ğ”¸ (Hom ğ”¸ X Y Ã— X) Y where
  toFun x := x.1 x.2
  exists_support' := by
    use âˆ…
    intro Ïƒ hÏƒ x
    simp only [rename_apply, Prod.rename_def]

end Hom

end RenamingSets
