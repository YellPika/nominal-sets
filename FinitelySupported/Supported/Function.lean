import FinitelySupported.Supported.Prod
import FinitelySupported.PermAction.Function

open PermAction

namespace Supported.Function

variable {ğ”¸ X Y Z} [PermAction ğ”¸ X] [PermAction ğ”¸ Y] [PermAction ğ”¸ Z]

@[fun_prop, simp]
lemma isHom_val (f : FS ğ”¸ (X â†’ Y)) : IsHom ğ”¸ f := by
  rcases f.property with âŸ¨A, âŸ¨hAâŸ©âŸ©
  use A
  intro Ï€ x hÏ€
  specialize hA Ï€â»Â¹
  simp only [Perm.inv_toFun, funext_iff, Function.perm_def, inv_inv] at hA
  rw [â†hA]
  Â· simp only [perm_mul, mul_inv_cancel, perm_one]
  Â· intro a ha
    nth_rw 1 [â† hÏ€ a ha]
    simp only [Perm.left_inverse]

/-- Currying for finitely-supported functions. -/
@[simps]
def curry [Supported ğ”¸ X] (f : FS ğ”¸ (X Ã— Y â†’ Z)) : FS ğ”¸ (X â†’ FS ğ”¸ (Y â†’ Z)) where
  val x := {
    val y := f (x, y)
    property := by
      apply Function.exists_isSupp_of_isHom
      fun_prop
  }
  property := by
    apply Function.exists_isSupp_of_isHom
    fun_prop

@[local fun_prop, simp]
lemma isHom_evalâ‚€ : IsHom ğ”¸ (fun p : FS ğ”¸ (X â†’ Y) Ã— X => p.1 p.2) := by
  use âˆ…
  intro Ï€ x hÏ€
  simp only [Prod.perm_def, FS.perm_val, Function.perm_def, perm_mul, inv_mul_cancel, perm_one]

@[fun_prop]
lemma isHom_eval
    {f : X â†’ FS ğ”¸ (Y â†’ Z)} (hf : IsHom ğ”¸ f)
    {g : X â†’ Y} (hg : IsHom ğ”¸ g)
    : IsHom ğ”¸ (fun x â†¦ f x (g x)) := by
  fun_prop

/-- Uncurrying for finitely-supported functions. -/
@[simps]
def eval : FS ğ”¸ (FS ğ”¸ (X â†’ Y) Ã— X â†’ Y) where
  val x := x.1 x.2
  property := by
    apply Function.exists_isSupp_of_isHom
    fun_prop

@[simp]
lemma supp_eval [Supported ğ”¸ X] [Supported ğ”¸ Y] : supp ğ”¸ (eval (ğ”¸ := ğ”¸) (X := X) (Y := Y)) = âˆ… := by
  have : IsSupp (âˆ… : Finset ğ”¸) (eval (ğ”¸ := ğ”¸) (X := X) (Y := Y)) := by
    constructor
    intro Ï€ hÏ€
    ext âŸ¨f, xâŸ©
    simp only [
      FS.perm_val, Function.perm_def, Prod.perm_def, eval_val,
      inv_inv, perm_mul, mul_inv_cancel, perm_one]
  have := supp_min _ this
  simp_all only [Finset.subset_empty]

lemma isSupp_apply
    {A : Finset ğ”¸} {f : FS ğ”¸ (X â†’ Y)} {x : X}
    (hf : IsSupp A f) (hx : IsSupp A x)
    : IsSupp A (f x) := by
  constructor
  intro Ï€ hÏ€
  simp only at hÏ€
  have hf := hf.eq Ï€ (by grind)
  simp only [FS.ext_iff, FS.perm_val] at hf
  replace hf := congr_fun hf (perm Ï€ x)
  simp only [Function.perm_def, perm_mul, inv_mul_cancel, perm_one] at hf
  rw [hf, hx.eq Ï€ (by grind)]

@[simp]
lemma isHom_iff (f : X â†’ FS ğ”¸ (Y â†’ Z)) : IsHom ğ”¸ f â†” IsHom ğ”¸ (fun x : X Ã— Y â†¦ f x.1 x.2) := by
  apply Iff.intro
  Â· intro hf
    fun_prop
  Â· intro hf
    apply FS.isHom_mk
    fun_prop

end Supported.Function
