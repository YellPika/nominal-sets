import NominalSets.Support.Basic

/-!
# Product Types

This file formalizes definitions and results about permutations and supports for product types.
-/

namespace NominalSets.Prod

variable {ğ”¸ X Y Z W : Type*} [PermAction ğ”¸ X] [PermAction ğ”¸ Y] [PermAction ğ”¸ Z] [PermAction ğ”¸ W]

/-!
## Permutations
-/

@[simps -isSimp]
instance : PermAction ğ”¸ (X Ã— Y) where
  perm Ï€ x := (perm Ï€ x.1, perm Ï€ x.2)
  perm_one := by simp only [perm_one, Prod.mk.eta, implies_true]
  perm_mul := by simp only [perm_mul, implies_true]

@[simp]
lemma perm_mk (Ï€ : Perm ğ”¸) (x : X) (y : Y) : perm Ï€ (x, y) = (perm Ï€ x, perm Ï€ y) := by rfl

@[simp]
lemma perm_fst (Ï€ : Perm ğ”¸) (x : X Ã— Y) : perm Ï€ (Prod.fst x) = Prod.fst (perm Ï€ x) := by
  cases x
  simp only [perm_mk]

@[simp]
lemma perm_snd (Ï€ : Perm ğ”¸) (x : X Ã— Y) : perm Ï€ (Prod.snd x) = Prod.snd (perm Ï€ x) := by
  cases x
  simp only [perm_mk]

instance [DiscretePermAction ğ”¸ X] [DiscretePermAction ğ”¸ Y] : DiscretePermAction ğ”¸ (X Ã— Y) where
  perm_discrete := by simp only [Prod.forall, Prod.perm_mk, perm_discrete, implies_true]

/-!
## Supports
-/

@[simp]
lemma isSupportOf_mk
    (A : Finset ğ”¸) (x : X) (y : Y)
    : IsSupportOf A (x, y) â†” IsSupportOf A x âˆ§ IsSupportOf A y := by
  simp only [isSupportOf_def, perm_def]
  grind

@[simp]
lemma isSupported_mk
    (x : X) (y : Y)
    : IsSupported ğ”¸ (x, y) â†” IsSupported ğ”¸ x âˆ§ IsSupported ğ”¸ y := by
  classical
  simp only [isSupported_def, isSupportOf_mk]
  apply Iff.intro
  Â· grind
  Â· rintro âŸ¨âŸ¨A, hAâŸ©, âŸ¨B, hBâŸ©âŸ©
    use A âˆª B
    apply And.intro
    Â· exact isSupportOf_union_left hA
    Â· exact isSupportOf_union_right hB

instance [Nominal ğ”¸ X] [Nominal ğ”¸ Y] : Nominal ğ”¸ (X Ã— Y) where
  supported := by simp only [Prod.forall, isSupported_mk, Nominal.supported, and_self, implies_true]

@[simp]
lemma supp_mk
    [DecidableEq ğ”¸] [Nominal ğ”¸ X] [Nominal ğ”¸ Y]
    (x : X) (y : Y)
    : supp ğ”¸ (x, y) = supp ğ”¸ x âˆª supp ğ”¸ y := by
  ext a
  simp only [mem_supp, isSupportOf_mk, and_imp, Finset.mem_union]
  apply Iff.intro
  Â· intro h
    simp only [or_iff_not_imp_left, not_forall, forall_exists_index]
    intro A hA ha B hB
    specialize h (A âˆª B) (isSupportOf_union_left hA) (isSupportOf_union_right hB)
    grind
  Â· grind

@[simp, fun_prop]
lemma isSupportedF_fst : IsSupportedF ğ”¸ (Prod.fst : X Ã— Y â†’ X) := by
  use âˆ…
  simp only [implies_true, perm_fst]

@[simp, fun_prop]
lemma isSupportedF_snd : IsSupportedF ğ”¸ (Prod.snd : X Ã— Y â†’ Y) := by
  use âˆ…
  simp only [implies_true, perm_snd]

@[fun_prop]
lemma isSupportedF_mk
    {f : X â†’ Y} (hf : IsSupportedF ğ”¸ f)
    {g : X â†’ Z} (hg : IsSupportedF ğ”¸ g)
    : IsSupportedF ğ”¸ (fun x â†¦ (f x, g x)) := by
  classical
  obtain âŸ¨A, hAâŸ© := hf
  obtain âŸ¨B, hBâŸ© := hg
  use A âˆª B
  intros
  simp (disch := grind) only [perm_mk, hA, hB]

/-!
## Equivariance
-/

@[simp, fun_prop]
lemma equivariant_fst : Equivariant ğ”¸ (Prod.fst : X Ã— Y â†’ X) := by
  constructor
  simp only [implies_true, perm_fst]

@[simp, fun_prop]
lemma equivariant_snd : Equivariant ğ”¸ (Prod.snd : X Ã— Y â†’ Y) := by
  constructor
  simp only [implies_true, perm_snd]

@[fun_prop]
lemma equivariant_mk
    {f : X â†’ Y} (hf : Equivariant ğ”¸ f)
    {g : X â†’ Z} (hg : Equivariant ğ”¸ g)
    : Equivariant ğ”¸ (fun x â†¦ (f x, g x)) := by
  constructor
  simp (disch := grind) only [perm_mk, hf.eq, hg.eq, implies_true]

end NominalSets.Prod
