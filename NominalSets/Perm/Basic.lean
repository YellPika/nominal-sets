import NominalSets.Perm.Defs
import Mathlib.Algebra.Group.Defs
import Mathlib.Logic.Equiv.List
import Mathlib.Data.Countable.Basic

/-!
# Permutations

This file provides lemmas related to finite permutations.
-/

namespace NominalSets.Perm

variable {ğ”¸ : Type*}

@[simp]
lemma coe_mk (Ï€ Ï€' : ğ”¸ â†’ ğ”¸) (hÏ€â‚ hÏ€â‚‚ hÏ€â‚ƒ) (x) : mk Ï€ Ï€' hÏ€â‚ hÏ€â‚‚ hÏ€â‚ƒ x = Ï€ x := rfl

@[ext]
lemma ext {Ï€â‚ Ï€â‚‚ : Perm ğ”¸} (h : âˆ€ a, Ï€â‚ a = Ï€â‚‚ a) : Ï€â‚ = Ï€â‚‚ := by
  rcases Ï€â‚ with âŸ¨Ï€â‚â‚, Ï€â‚â‚‚, hÏ€â‚â‚, hÏ€â‚â‚‚, hÏ€â‚â‚ƒâŸ©
  rcases Ï€â‚‚ with âŸ¨Ï€â‚‚â‚, Ï€â‚‚â‚‚, hÏ€â‚‚â‚, hÏ€â‚‚â‚‚, hÏ€â‚ƒâ‚ƒâŸ©
  simp only [coe_mk, mk.injEq] at âŠ¢ h
  grind

@[simp]
lemma coe_injective (Ï€ : Perm ğ”¸) : Function.Injective Ï€ := by
  apply Function.LeftInverse.injective (g := (Ï€â»Â¹ : Perm ğ”¸))
  simp only [Function.LeftInverse, left_inverse, implies_true]

@[simp]
lemma coe_inj (Ï€ : Perm ğ”¸) (a b : ğ”¸) : Ï€ a = Ï€ b â†” a = b := by
  apply Iff.intro
  Â· apply coe_injective
  Â· grind

@[simp]
lemma inv_injective : Function.Injective (Â· â»Â¹ : Perm ğ”¸ â†’ Perm ğ”¸) := by
  intro Ï€â‚ Ï€â‚‚ hÏ€
  simp only at hÏ€
  simp only [Perm.ext_iff] at âŠ¢ hÏ€
  intro a
  specialize hÏ€ (Ï€â‚ a)
  simp only [left_inverse] at hÏ€
  nth_rw 2 [hÏ€]
  simp only [right_inverse]

@[simp]
lemma inv_inj {Ï€â‚ Ï€â‚‚ : Perm ğ”¸} : Ï€â‚â»Â¹ = Ï€â‚‚â»Â¹ â†” Ï€â‚ = Ï€â‚‚ := by
  apply Iff.intro
  Â· apply inv_injective
  Â· grind

@[simp]
lemma eta {Ï€â‚ Ï€â‚‚ : Perm ğ”¸} : (Ï€â‚ : ğ”¸ â†’ ğ”¸) = Ï€â‚‚ â†” Ï€â‚ = Ï€â‚‚ := by
  apply Iff.intro
  Â· intro h
    ext
    rw [h]
  Â· grind

instance : Group (Perm ğ”¸) where
  one_mul _ := rfl
  mul_assoc _ _ _ := rfl
  mul_one _ := rfl
  inv_mul_cancel _ := by
    ext
    simp only [mul_coe, left_inverse, one_coe]

@[simp]
lemma inv_one : 1â»Â¹ = (1 : Perm ğ”¸) := by rfl

@[simp]
lemma mul_assoc (Ï€â‚ Ï€â‚‚ Ï€â‚ƒ : Perm ğ”¸) : Ï€â‚ * (Ï€â‚‚ * Ï€â‚ƒ) = Ï€â‚ * Ï€â‚‚ * Ï€â‚ƒ := by rfl

@[simp]
lemma swap_swap [DecidableEq ğ”¸] (a b : ğ”¸) : swap a b * swap a b = 1 := by
  ext
  simp only [mul_coe, swap_coe, left_eq_ite_iff, one_coe]
  grind

@[simp]
lemma swap_swap_l [DecidableEq ğ”¸] (Ï€) (a b : ğ”¸) : Ï€ * swap a b * swap a b  = Ï€ := by
  simp only [â† mul_assoc, swap_swap, mul_one]

lemma swap_comm [DecidableEq ğ”¸] (a b : ğ”¸) : swap a b = swap b a := by
  ext
  simp only [swap_coe]
  grind

@[simp]
lemma inv_swap [DecidableEq ğ”¸] (a b : ğ”¸) : (swap a b)â»Â¹ = swap a b := rfl

@[simp]
lemma swap_eq [DecidableEq ğ”¸] (a : ğ”¸) : swap a a = 1 := by
  ext
  simp only [swap_coe, one_coe]
  grind

lemma swap_mul
    [DecidableEq ğ”¸] (a b : ğ”¸) (Ï€ : Perm ğ”¸)
    : swap a b * Ï€ = Ï€ * swap (Ï€â»Â¹ a) (Ï€â»Â¹ b) := by
  ext c
  simp only [mul_coe, swap_coe]
  by_cases hac : a = Ï€ c
  Â· simp only [hac, â†“reduceIte, left_inverse, right_inverse]
  Â· simp only [hac, â†“reduceIte]
    by_cases hbc : b = Ï€ c
    Â· have : Ï€â»Â¹ a â‰  c := by
        rintro rfl
        simp only [right_inverse, not_true_eq_false] at hac
      simp only [hbc, â†“reduceIte, this, left_inverse, right_inverse]
    Â· simp only [hbc, â†“reduceIte]
      have hac' : Ï€â»Â¹ a â‰  c := by
        rintro rfl
        simp only [Perm.right_inverse, not_true_eq_false] at hac
      simp only [hac', â†“reduceIte, coe_inj, right_eq_ite_iff]
      rintro rfl
      simp only [Perm.right_inverse, not_true_eq_false] at hbc

lemma mul_swap
    [DecidableEq ğ”¸] (a b : ğ”¸) (Ï€ : Perm ğ”¸)
    : Ï€ * swap a b = swap (Ï€ a) (Ï€ b) * Ï€ := by
  simp only [swap_mul, left_inverse]

@[simp]
lemma seq_append [DecidableEq ğ”¸] (xs ys : List (ğ”¸ Ã— ğ”¸)) : seq (xs ++ ys) = seq xs * seq ys := by
  ext x
  induction xs with
  | nil => simp only [List.nil_append, seq, one_mul]
  | cons ab xs ih => simp only [List.cons_append, seq, mul_coe, ih, swap_coe]

lemma exists_seq [DecidableEq ğ”¸] (Ï€ : Perm ğ”¸) : âˆƒxs, Ï€ = seq xs := by
  obtain âŸ¨X, hXâŸ© := Ï€.has_supp
  induction X using Finset.induction generalizing Ï€ with
  | empty =>
    simp only [Finset.notMem_empty, not_false_eq_true, forall_const] at hX
    use []
    ext x
    simp only [seq, one_coe]
    apply hX
  | insert a s ha ih =>
    by_cases hÏ€ : Ï€ a = a
    Â· apply ih Ï€
      intro b hb
      by_cases hab : b = a
      Â· simp only [hab, hÏ€]
      Â· apply hX
        simp only [Finset.mem_insert, hab, hb, or_self, not_false_eq_true]
    Â· specialize ih (swap a (Ï€ a) * Ï€) ?_
      Â· intro b hb
        by_cases hab : b = a
        Â· simp only [hab, mul_coe, swap_coe, â†“reduceIte, ite_eq_right_iff]
          grind
        Â· specialize hX b
          have hab' : a â‰  b := by grind
          simp only [
            Finset.mem_insert, hab, hb, or_self,
            not_false_eq_true, forall_const] at hX
          simp only [mul_coe, hX, swap_coe, hab', â†“reduceIte, ite_eq_right_iff, imp_false, ne_eq]
          rintro rfl
          simp only [coe_inj] at hX
          contradiction
      Â· rcases ih with âŸ¨xs, hxsâŸ©
        use (a, Ï€ a) :: xs
        simp only [seq, â† hxs, mul_assoc, swap_swap, one_mul]

instance [Countable ğ”¸] : Countable (Perm ğ”¸) := by
  classical
  have lem (Ï€ : Perm ğ”¸) := exists_seq Ï€
  choose toList prop using lem
  apply Function.Injective.countable (f := toList)
  intro Ï€â‚ Ï€â‚‚ hÏ€
  have := congr_arg seq hÏ€
  simpa only [â† prop] using this

end NominalSets.Perm
