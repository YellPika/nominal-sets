import Mathlib.Tactic.FunProp
import Mathlib.Tactic.TypeStar
import Mathlib.Data.Finset.Defs
import Mathlib.Data.Finset.Empty
import Mathlib.Data.Set.Finite.Basic

/-!
# Permutations

This file defines finite permutations and related operations.
-/

namespace NominalSets

/-- The type of finite permutations on a type `ğ”¸`. -/
structure Perm (ğ”¸ : Type*) where
  /-- The forward permutation. -/
  toFun : ğ”¸ â†’ ğ”¸
  /-- The inverse permutation. -/
  invFun : ğ”¸ â†’ ğ”¸
  /-- `invFun` is a right inverse of `toFun`. -/
  right_inverse' (a : ğ”¸) : toFun (invFun a) = a
  /-- `invFun` is a left inverse of `toFun`. -/
  left_inverse' (a : ğ”¸) : invFun (toFun a) = a
  /-- `toFun` is finitely supported. -/
  has_supp' : âˆƒA : Finset ğ”¸, âˆ€a âˆ‰ A, toFun a = a

namespace Perm

variable {ğ”¸ : Type*}

instance : FunLike (Perm ğ”¸) ğ”¸ ğ”¸ where
  coe := toFun
  coe_injective' Ï€â‚ Ï€â‚‚ hÏ€ := by
    rcases Ï€â‚ with âŸ¨f, f', hfâ‚, hfâ‚‚, hfâ‚ƒâŸ©
    rcases Ï€â‚‚ with âŸ¨g, g', hgâ‚, hgâ‚‚, hgâ‚ƒâŸ©
    dsimp only at hÏ€
    simp only [hÏ€, mk.injEq, true_and]
    ext a
    nth_rw 2 [â† hfâ‚ a]
    simp only [hÏ€, hgâ‚‚]

instance : Inv (Perm ğ”¸) where
  inv Ï€ := {
    toFun := Ï€.invFun
    invFun := Ï€.toFun
    right_inverse' := by simp only [left_inverse', implies_true]
    left_inverse' := by simp only [right_inverse', implies_true]
    has_supp' := by
      rcases Ï€.has_supp' with âŸ¨X, hXâŸ©
      use X
      intro a ha
      specialize hX a ha
      nth_rw 1 [â†hX]
      simp only [left_inverse']
  }

/-- A simps projection for function coercion. -/
def Simps.coe (Ï€ : Perm ğ”¸) : ğ”¸ â†’ ğ”¸ := Ï€

/-- A simps projection for inverse function coercion. -/
def Simps.inv_coe (Ï€ : Perm ğ”¸) : ğ”¸ â†’ ğ”¸ := (Ï€â»Â¹ : Perm ğ”¸)

initialize_simps_projections Perm (toFun â†’ coe, invFun â†’ inv_coe)

@[simp]
lemma right_inverse (Ï€ : Perm ğ”¸) (a : ğ”¸) : Ï€ (Ï€â»Â¹ a) = a :=
  Ï€.right_inverse' a

@[simp]
lemma left_inverse (Ï€ : Perm ğ”¸) (a : ğ”¸) : Ï€â»Â¹ (Ï€ a) = a :=
  Ï€.left_inverse' a

lemma has_supp (Ï€ : Perm ğ”¸) : âˆƒA : Finset ğ”¸, âˆ€a âˆ‰ A, Ï€ a = a :=
  Ï€.has_supp'

@[simps]
instance : One (Perm ğ”¸) where
  one := {
    toFun a := a
    invFun a := a
    right_inverse' _ := rfl
    left_inverse' _ := rfl
    has_supp' := by
      simp only [implies_true, exists_const_iff, and_true]
      exact âŸ¨âˆ…âŸ©
  }

@[simps]
instance : Mul (Perm ğ”¸) where
  mul Ï€â‚ Ï€â‚‚ := {
    toFun a := Ï€â‚ (Ï€â‚‚ a)
    invFun a := Ï€â‚‚â»Â¹ (Ï€â‚â»Â¹ a)
    right_inverse' := by simp only [right_inverse, implies_true]
    left_inverse' := by simp only [left_inverse, implies_true]
    has_supp' := by
      classical
      rcases Ï€â‚.has_supp with âŸ¨Xâ‚, hXâ‚âŸ©
      rcases Ï€â‚‚.has_supp with âŸ¨Xâ‚‚, hXâ‚‚âŸ©
      use Xâ‚ âˆª Xâ‚‚
      simp only [Finset.mem_union, not_or, and_imp]
      intro a haâ‚ haâ‚‚
      rw [hXâ‚‚ a haâ‚‚, hXâ‚ a haâ‚]
  }

/-- The permutation that simply swaps two elements. -/
@[simps]
def swap (a b : ğ”¸) [DecidableEq ğ”¸] : Perm ğ”¸ where
  toFun c :=
    if a = c then b else
    if b = c then a else c
  invFun c :=
    if a = c then b else
    if b = c then a else c
  right_inverse' _ := by grind only [cases Or]
  left_inverse' _ := by grind only [cases Or]
  has_supp' := by
    use {a, b}
    intro c hc
    simp only [Finset.mem_insert, Finset.mem_singleton, not_or] at hc
    grind only

/-- A sequence of pairs induces a permutation constructed as a sequences of `swap`s. -/
@[simp]
def seq [DecidableEq ğ”¸] : List (ğ”¸ Ã— ğ”¸) â†’ Perm ğ”¸
  | [] => 1
  | (a, b) :: xs => swap a b * seq xs

end Perm

end NominalSets
